group 'ca.utoronto.ece419'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile group: 'org.clapper', name: 'javautil', version: '3.2.0'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.sf.proguard', name: 'proguard-gradle', version: '5.3.3'
    }
}

sourceSets {
    secret {
        compileClasspath = configurations.compile
        java {
            srcDirs 'src'
        }
    }
    release {
        compileClasspath = configurations.compile
        java {
            srcDirs 'src'
            exclude '**/secret/**'
        }
    }
}

task buildSecret(type: Jar) {
    manifest {
        attributes 'Main-Class': 'ca.utoronto.ece419.tester.Main'
    }

    archiveName = 'm1-tester.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }}
    from sourceSets.secret.output
    with jar
}

task buildRelease(type: proguard.gradle.ProGuardTask) {
    dependsOn buildSecret

    configuration 'proguard.txt'

    injars 'build/libs/m1-tester.jar'
    outjars 'build/libs/m1-tester-release.jar'
    forceprocessing
}

task buildAll() {
    dependsOn buildSecret
    dependsOn buildRelease
}
